char = meta <whitespace> [endsnippet] summary <'\n\n'> stats resistances <#'.*'>

<whitespace> = #'\s*'
text = {#'[0-9a-zA-Z,@\-\+;:!\(\)\{\}\\"\*]+' | '.' | '/' | '\'' | whitespace}
number = #'[0-9\-\+]+'
<date> = #'[a-zA-Z]+ [0-9]+, [0-9]+'

meta = <whitespace 'Dungeon Crawl Stone Soup version '> version <' ('> interface <') character file.'>
version = #'[0-9a-zA-Z-\.]+'
interface = #'[a-z]+'

<endsnippet> = scoreline beginning (godsummary / <text>) (ending / <text>) <[text] '.\n\n'>

<scoreline> = score <#'.*HPs\)'>
score = number

<beginning> = <#'\s*Began.*on '> startdate <'.\n'>
startdate = date

<godsummary> = <#'\s*Was [a-z]+ '> godtitle <' of ' text '.\n'>
godtitle = text

<ending> = <text ' on '> enddate <('.' | '!') '\n'>
enddate = date

summary = name-and-title <'('> character <')' whitespace 'Turns: '> turns <', Time: '> time
name-and-title = #'[^(]+'
character = #'[A-Z][a-zA-Z ]+'
turns = number
time = #'([0-9]+, )?[0-9:]+'

stats = stats1 stats2 stats3 [stats4]

<stats1> = <whitespace 'HP' whitespace> hp <'/'> mhp [<whitespace '('> mmhp <')'>] <whitespace 'AC' whitespace> ac <whitespace 'Str' whitespace> str [<whitespace '('> mstr <')'>] <whitespace 'XL:' whitespace> xl [<whitespace 'Next:' whitespace> percent-of-next-xl <'%'>]
hp = number
mhp = number
mmhp = number
ac = number
str = number
mstr = number
xl = number
percent-of-next-xl = number

<stats2> = <whitespace 'MP' whitespace> mp <'/'> mmp [<whitespace '('> mmmp <')'>] <whitespace 'EV' whitespace> ev <whitespace 'Int' whitespace> int [<whitespace '('> mint <')'>] <whitespace 'God: '> [<whitespace> god [ <' ['> piety <']'> ]]
mp = number
mmp = number
mmmp = number
ev = number
int = number
mint = number
god = !'Gold' text
piety = #'[\.\*]+'

<stats3> = <whitespace 'Gold' whitespace> gold <whitespace 'SH' whitespace> sh <whitespace 'Dex' whitespace> dex [<whitespace '('> mdex <')'>] <whitespace 'Spells:' whitespace> spells <' memorised,' whitespace> spell-levels-left <#' levels? left'>
gold = number
sh = number
dex = number
mdex = number
spells = number
spell-levels-left = number

<stats4> = <whitespace 'Lives: '> lives <', deaths: '> deaths
lives = number
deaths = number

resistances = <'\n'> {resist-line}
<resist-line> = <'\n'> resist-entry resist-entry <#'[^\n]*'>
<resist-entry> = ((rF | sInv | rC | Ward | rN | Cons | rPois | rCorr | rElec | Clar | SustAb | spirit | rMut | stasis | rRot | Fly | Sap | MR) <whitespace>) / <whitespace>
rF     = <'Res.Fire' | 'rFire'>      resist-val
sInv   = <'See Invis.' | 'SeeInvis'> resist-val
rC     = <'Res.Cold' | 'rCold'>      resist-val
Ward   = <'Warding'>                 resist-val
rN     = <'Life Prot.' | 'rNeg'>     resist-val
Cons   = <'Conserve'>                resist-val
rPois  = <'Res.Poison' | 'rPois'>    resist-val
rCorr  = <'Res.Corr.' | 'rCorr'>     resist-val
rElec  = <'Res.Elec.' | 'rElec'>     resist-val
Clar   = <'Clarity'>                 resist-val
SustAb = <'Sust.Abil.' | 'SustAb'>   resist-val
spirit = <'Spirit.Shd' | 'Spirit'>   resist-val
rMut   = <'Res.Mut.' | 'rMut'>       resist-val
stasis = <'Stasis'>                  resist-val
rRot   = <'Res.Rott.' | 'rRot'>      resist-val
Fly    = <'Flight'>                  resist-val
Sap    = <'Saprovore' | 'Saprov'>    resist-val
MR     = <'MR'>                      resist-val
resist-val = <whitespace (':' | '') whitespace> #'([\+x\.] ?)+'
